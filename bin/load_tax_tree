#!/usr/bin/env perl
#
# PODNAME: load_tax_tree
# ABSTRACT: loads the NCBI taxonomy tree
# jt6 20150204 WTSI

# vim: ft=perl :

use strict;
use warnings;

use Getopt::Long::Descriptive;
use Pod::Usage;
use Config::General;
use Try::Tiny;
use Carp qw( croak );
use Archive::Tar;
use File::Temp;
use DateTime;
use Digest::MD5;

use Bio::HICF::Schema;
use Bio::Metadata::TaxTree;

#-------------------------------------------------------------------------------
# boilerplate

# define the accepted options
my ( $opt, $usage ) = describe_options(
  '%c %o <filename>',
  [ 'config|c=s', 'path to the configuration file' ],
  [ 'help|h',     'print usage message' ],
);

# show the POD as usage information
pod2usage( { -verbose => 2, -exitval => 0 } )
  if $opt->help;

# take the paths to the config either from the options or from an environment
# variable
my $config_file = $opt->config || $ENV{HICF_CONFIG};

_usage($usage, 'ERROR: you must specify a configuration file')
  unless defined $config_file;

_usage($usage, "ERROR: no such configuration file ($config_file)")
  unless -f $config_file;

#-------------------------------------------------------------------------------
# load configuration

my $cg;
try {
  $cg = Config::General->new($config_file);
} catch {
  croak "ERROR: there was a problem reading the config file ($config_file): $_";
};

my %config = $cg->getall;

#-------------------------------------------------------------------------------
# unpack the dump files

my $taxdump = $config{taxdump}->{file};

my $tar = Archive::Tar->new( $taxdump );
my $temp_dir = File::Temp->newdir;

my $names = "$temp_dir/names.dmp";
my $nodes = "$temp_dir/nodes.dmp";

my $status = $tar->extract_file( 'names.dmp', $names );
croak 'ERROR: failed to extract "names.dmp" from the tardump.tar file'
  unless $status;

$status = $tar->extract_file( 'nodes.dmp', $nodes );
croak 'ERROR: failed to extract "nodes.dmp" from the tardump.tar file'
  unless $status;

# build a tree
my $tt = Bio::Metadata::TaxTree->new( names_file => $names,
                                      nodes_file => $nodes );

$tt->build_tree;

# get a database connection
my $schema = Bio::HICF::Schema->connect( @{ $config{database}->{connect_info} } );

# load it
try {
  $schema->load_tax_tree($tt);
} catch {
  croak "ERROR: there was a problem loading the tax tree:\n$_";
};

# record the dump file details

# calculate the checksum first
my $digest = Digest::MD5->new;
open ( my $fh, $config{taxdump}->{file} )
  or croak "ERROR: couldn't read taxdump file to calculate checksum: $!";
$digest->addfile($fh);

$schema->add_external_resource(
  {
    name         => 'taxdump',
    source       => $config{taxdump}->{url},
    retrieved_at => DateTime->now,
    checksum     => $digest->hexdigest,
  }
);

exit 0;

#-------------------------------------------------------------------------------
#- functions -------------------------------------------------------------------
#-------------------------------------------------------------------------------

sub _usage {
  my ( $usage, $msg ) = @_;

  print STDERR "$msg\n";
  print $usage->text;
  exit 1;
}

#-------------------------------------------------------------------------------

__END__

=head1 SYNOPSIS

 shell% load_tax_tree -c hicf_script_configuration.conf

=head1 DESCRIPTION

This script reads the NCBI taxonomy tree dump files (C<names.dmp> and
C<nodes.dmp>) and loads the resulting tree into the C<taxonomy> table in the
HICF sample database. The script requires one argument, the path to a
configuration file.

Before loading the new taxonomy data, the C<taxonomy> table is first emptied.
If there is an error during loading, the script tries to roll back the
truncation and any subsequent loading.

=head1 OPTIONS

=over 4

=item -h --help

display help text

=item -c --config

configuration file. Required.

=back

=head1 CONFIGURATION

The configuration file must specify the database connection parameters
(C<database>) and the canonical URL for the tax dump file (C<taxdump>).

 <database>
   connect_info dbi:mysql:name=hicf;port=3341;host=dbhost
   connect_info "hicf"
   connect_info "password"
 </database>
 <taxdump>
   url ftp://ftp.ncbi.nlm.nih.gov/pub/taxonomy/taxdump.tar.gz
 </taxdump>

=head1 SEE ALSO

L<Bio::Metadata::TaxTree>
L<Bio::HICF::Schema>

=head1 CONTACT

path-help@sanger.ac.uk

=cut

